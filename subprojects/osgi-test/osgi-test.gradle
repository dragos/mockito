plugins {
    id 'java'
}

apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "osgi-test-bundles.gradle"

description = "Test suite for OSGi framework with Mockito"

dependencies {
    testImplementation project.rootProject
    testImplementation libraries.junit4
    testImplementation libraries.osgi

    testRuntimeOnly libraries.equinox
}

tasks.javadoc.enabled = false

configurations {
    testRuntimeBundles
}

dependencies {
    testRuntimeBundles project.rootProject
    testRuntimeBundles libraries.bytebuddy
    testRuntimeBundles libraries.objenesis
    testRuntimeBundles tasks.testBundle.outputs.files
    testRuntimeBundles tasks.otherBundle.outputs.files
}

class RuntimeBundlesProvider implements CommandLineArgumentProvider {
    @InputFiles
    @PathSensitive(PathSensitivity.RELATIVE)
    FileTree files

    @Override
    Iterable<String> asArguments() {
        println("in: ${files}")
        String[] absolutePaths = files.stream().map {it.absolutePath}.toArray()
        ["-DtestRuntimeBundles=${CollectionUtils.join(File.pathSeparator, absolutePaths)}".toString()]
    }
}

test {
    jvmArgumentProviders.add(
        new RuntimeBundlesProvider(files: configurations.testRuntimeBundles.asFileTree)
    )
//    println(allJvmArgs)
    dependsOn configurations.testRuntimeBundles
    inputs.files(sourceSets.testBundle.allSource).withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.files(sourceSets.otherBundle.allSource).withPathSensitivity(PathSensitivity.RELATIVE)
//    systemProperty 'testRuntimeBundles', configurations.testRuntimeBundles.asPath
    useJUnit()
}

